#
# $Id: LXPepNovo2Taggor.awk 1362 2011-08-22 14:30:45Z viari $
#
# convert pepnovo output to taggor format
# and filter out some tags if needed
#
# usage :
# awk [-v FILTER='X|N|C|NC'] [-v NMAX=<integer>] [-v SMIN=<float>] -f pepNovo2Taggor.awk <pepnovo_output>
#
# optional arguments:
#
# FILTER='X|N|C|NC' : filter out tags with MN or MC == 0 (use 'X' to disable filtering)
#
# NMAX=<integer> : keep at most <integer> tags  (dft = 10)
#
# SMIN=<float> : keep only tags whose score >= SMIN (dft = 0 : don't filter)
#

BEGIN {
  if (FILTER == "") FILTER = "NC"
  if (NMAX == "")   NMAX = 10
  if (SMIN == "")   SMIN = 0
  
  FILTER_N = (FILTER ~ "N")
  FILTER_C = (FILTER ~ "C")

  MH3O = 19.0183896
}

/^#/ {                  # skip comments
  next
}

/>>.*scans:/ {          # for mgf data : use spectrum_num and scan_num
  scan = $NF
  gsub("[^0-9]", "", scan)
  name = $4 "." scan
  state = 1
  next
}

/>>/ {                  # for pkl or dta data : use spectrum name
  name = $3
  state = 1
  next
}

(state == 1) {          # start of tag list
  if (nbsp++ > 0)
    print "%EndOfTags"
  print "# Tags generated by PepNovo+"
  nbtag = 0
  state = 2
}

(state == 2) && (NF >= 8) {
  mn = $4
  if ((mn == 0) && FILTER_N) next

  mc = ($5 > 0 ? $5 - MH3O : $5)    # bug in pepnovo ?
  if ((mc == 0) && FILTER_C) next

  scor = $2
  if (scor < SMIN) next
  
  nbtag++
  
  if ((NMAX > 0) && (nbtag > NMAX)) next
  
  tag = $8
  gsub("\\+[0-9]+", "", tag) # remove modif
  
  print name "." nbtag, mn, mc, tag, $6, scor
  next
}

END {
  if (nbsp++ > 0)
    print "%EndOfTags"
}
